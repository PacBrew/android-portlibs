# Maintainer: cpasjuste <cpasjuste@gmail.com>

_android_arch=aarch64

pkgname=android-${_android_arch}-libcurl
pkgver=7.80.0
pkgrel=1
pkgdesc='An URL retrieval utility and library'
arch=('any')
url='https://curl.haxx.se/'
license=('curl')
source=("https://curl.haxx.se/download/curl-${pkgver}.tar.xz")
sha256sums=('a132bd93188b938771135ac7c1f3ac1d3ce507c1fcbef8c471397639214ae2ab')
options=(!strip libtool staticlibs)
depends=("android-${_android_arch}-openssl" "android-${_android_arch}-zlib")
makedepends=('android-configure')
groups=('android-${_android_arch}-portlibs')

build() {
  cd curl-$pkgver

  source /opt/pacbrew/android/android-env.sh ${_android_arch}

  autoreconf -fi

  android-configure \
    --with-openssl --disable-manual

  make $MAKEFLAGS -C lib

}

package() {
  cd curl-$pkgver

  source /opt/pacbrew/android/android-env.sh ${_android_arch}

  make DESTDIR="$pkgdir" -C lib install
  make DESTDIR="$pkgdir" -C include install
  make DESTDIR="$pkgdir" install-binSCRIPTS
}

# Maintainer: Gonzalo Exequiel Pedone <hipersayan DOT x AT gmail DOT com>
# Contributor: drakkan <nicola.murino at gmail dot com>
# Contributor: cpasjuste <cpasjuste@gmail.com>

_android_arch=aarch64

pkgname=android-${_android_arch}-libwebp
pkgver=1.2.2
pkgrel=1
pkgdesc="WebP library and conversion tools (android)"
arch=('any')
url="https://developers.google.com/speed/webp/"
license=("BSD")
depends=("android-${_android_arch}-libjpeg-turbo"
         "android-${_android_arch}-libpng")
options=(!strip !buildflags staticlibs !emptydirs)
makedepends=('android-configure')
groups=('android-${_android_arch}-portlibs')
source=("https://github.com/webmproject/libwebp/archive/v${pkgver}.tar.gz")
md5sums=('cafb3f64313daa7492cd08c06dd76801')

prepare() {
    cd "$srcdir/libwebp-${pkgver}"
    source /opt/pacbrew/android/android-env.sh ${_android_arch}

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "$srcdir/libwebp-${pkgver}"
    source /opt/pacbrew/android/android-env.sh ${_android_arch}
    
    android-configure \
        --enable-swap-16bit-csp \
        --enable-libwebp{mux,demux,decoder,extras}

    make $MAKEFLAGS
}

package() {
    cd "$srcdir/libwebp-${pkgver}"
    source /opt/pacbrew/android/android-env.sh ${_android_arch}

    make DESTDIR="$pkgdir" install
    rm -r "${pkgdir}"/${ANDROID_PREFIX_BIN}
    rm -r "${pkgdir}"/${ANDROID_PREFIX_SHARE}
    ${ANDROID_STRIP} -g "$pkgdir"/${ANDROID_PREFIX_LIB}/*.a
}

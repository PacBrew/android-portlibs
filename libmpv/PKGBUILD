# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=android-${CARCH}-libmpv
pkgver=0.34.1
pkgrel=1
pkgdesc='Command line video player (library only)'
arch=('any')
url='https://mpv.io/'
license=('GPL')
options=(!strip libtool staticlibs)
source=("libmpv-${pkgver}.tar.gz::https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz")
sha256sums=('32ded8c13b6398310fa27767378193dc1db6d78b006b70dbcbd3123a1445e746')
makedepends=('android-environment' 'android-pkg-config')
depends=("android-${CARCH}-sdl2" "android-${CARCH}-ffmpeg")
groups=('android-${CARCH}-portlibs')

build() {
  cd mpv-$pkgver

  source /opt/pacbrew/android/android-env.sh ${CARCH}

  ./bootstrap.py

  TARGET=${CARCH} ./waf configure --prefix="${ANDROID_PREFIX}" \
    --disable-libmpv-shared --enable-libmpv-static --disable-cplayer \
    --disable-iconv --disable-jpeg --disable-libavdevice \
    --enable-sdl2 --disable-sdl2-audio --disable-sdl2-gamepad --disable-sdl2-video \
    --enable-opensles

  ./waf build
}

package() {
  cd mpv-$pkgver
  source /opt/pacbrew/android/android-env.sh ${CARCH}
  
  ./waf install --destdir=$pkgdir
  ${ANDROID_STRIP} -g "$pkgdir"/${ANDROID_PREFIX_LIB}/*.a
}


# Maintainer: Gonzalo Exequiel Pedone <hipersayan DOT x AT gmail DOT com>
# Contributor: cpasjuste <cpasjuste@gmail.com>

_android_arch=aarch64

pkgname=android-${_android_arch}-libjpeg-turbo
pkgver=2.1.3
pkgrel=1
arch=('any')
pkgdesc="JPEG image codec with accelerated baseline compression and decompression (android)"
license=("custom")
url="http://libjpeg-turbo.virtualgl.org"
depends=('android-ndk')
makedepends=('android-cmake')
groups=('android-${_android_arch}-portlibs')
options=(!strip !buildflags staticlibs !emptydirs)
source=("http://downloads.sourceforge.net/libjpeg-turbo/libjpeg-turbo-$pkgver.tar.gz")
md5sums=('85244dedeaf06f636a9e7ddea6d236d8')

build() {
    cd "${srcdir}"/libjpeg-turbo-${pkgver}
    source /opt/pacbrew/android/android-env.sh ${_android_arch}

    android-cmake \
        -DWITH_JPEG8=ON \
        .
    make $MAKEFLAGS
}

package() {
    cd "${srcdir}"/libjpeg-turbo-${pkgver}
    source /opt/pacbrew/android/android-env.sh ${_android_arch}

    make DESTDIR="$pkgdir" install
    rm -r "${pkgdir}"/${ANDROID_PREFIX_BIN}
    rm -r "${pkgdir}"/${ANDROID_PREFIX_SHARE}
    ${ANDROID_STRIP} -g "$pkgdir"/${ANDROID_PREFIX_LIB}/*.a
}


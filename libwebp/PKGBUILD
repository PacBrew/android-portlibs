# Maintainer: Gonzalo Exequiel Pedone <hipersayan DOT x AT gmail DOT com>
# Contributor: drakkan <nicola.murino at gmail dot com>
# Contributor: cpasjuste <cpasjuste@gmail.com>

pkgname=android-${CARCH}-libwebp
pkgver=1.2.2
pkgrel=1
pkgdesc="WebP library and conversion tools (android)"
arch=('any')
url="https://developers.google.com/speed/webp/"
license=("BSD")
depends=("android-${CARCH}-libjpeg-turbo"
         "android-${CARCH}-libpng")
options=(!strip !buildflags staticlibs !emptydirs)
makedepends=('android-configure')
groups=('android-${CARCH}-portlibs')
source=("https://github.com/webmproject/libwebp/archive/v${pkgver}.tar.gz")
md5sums=('cafb3f64313daa7492cd08c06dd76801')

prepare() {
    cd "$srcdir/libwebp-${pkgver}"
    source /opt/pacbrew/android/android-env.sh ${CARCH}

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "$srcdir/libwebp-${pkgver}"
    source /opt/pacbrew/android/android-env.sh ${CARCH}
    
    android-configure \
        --enable-swap-16bit-csp \
        --enable-libwebp{mux,demux,decoder,extras}

    make $MAKEFLAGS
}

package() {
    cd "$srcdir/libwebp-${pkgver}"
    source /opt/pacbrew/android/android-env.sh ${CARCH}

    make DESTDIR="$pkgdir" install
    rm -r "${pkgdir}"/${ANDROID_PREFIX_BIN}
    rm -r "${pkgdir}"/${ANDROID_PREFIX_SHARE}
    ${ANDROID_STRIP} -g "$pkgdir"/${ANDROID_PREFIX_LIB}/*.a
}

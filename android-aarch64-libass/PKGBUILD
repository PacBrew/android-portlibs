# Maintainer: cpasjuste <cpasjuste@gmail.com>

_android_arch=aarch64

pkgname=android-${_android_arch}-libass
pkgver=0.14.0
pkgrel=1
pkgdesc='A portable subtitle renderer'
arch=('any')
url="https://github.com/libass/libass"
license=('custom: ISC')
options=(!strip staticlibs)
depends=("android-${_android_arch}-freetype" "android-${_android_arch}-fribidi")
makedepends=('android-configure')
source=("https://github.com/libass/libass/releases/download/$pkgver/libass-$pkgver.tar.xz")
sha256sums=('881f2382af48aead75b7a0e02e65d88c5ebd369fe46bc77d9270a94aa8fd38a2')
groups=('android-${_android_arch}-portlibs')

build() {
  cd libass-$pkgver

  source /opt/pacbrew/android/android-env.sh ${_android_arch}

  android-configure \
    --disable-require-system-font-provider
  make $MAKEFLAGS
}

package() {
  cd libass-$pkgver
  make DESTDIR="$pkgdir" install
  ${ANDROID_STRIP} -g "$pkgdir"/${ANDROID_PREFIX_LIB}/*.a
}

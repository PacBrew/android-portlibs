# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=android-${CARCH}-sdl2
pkgver=2.24.1
pkgrel=1
pkgdesc="A library for portable low-level access to video, audio, joysticks..."
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('android-cmake')
groups=('android-${CARCH}-portlibs')
source=("https://github.com/libsdl-org/SDL/releases/download/release-$pkgver/SDL2-$pkgver.tar.gz")
sha256sums=('bc121588b1105065598ce38078026a414c28ea95e66ed2adab4c44d80b309e1b')

build() {
  cd SDL2-$pkgver
  source /opt/pacbrew/android/android-env.sh ${CARCH}

  mkdir -p build && cd build

  android-cmake ..
  make $MAKEFLAGS
}

package() {
  cd SDL2-$pkgver/build
  source /opt/pacbrew/android/android-env.sh ${CARCH}

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "${pkgdir}"/${ANDROID_PREFIX_SHARE}
}

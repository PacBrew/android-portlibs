# Maintainer: Gonzalo Exequiel Pedone <hipersayan DOT x AT gmail DOT com>
# Contributor: Karl-Felix Glatzer <karl[dot]glatzer[at]gmx[dot]de>
# Contributor: cpasjuste <cpasjuste@gmail.com>

pkgname=android-${CARCH}-fribidi
pkgver=1.0.11
pkgrel=2
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm (android)"
arch=('any')
url="http://fribidi.org"
license=('LGPL')
depends=('android-ndk')
options=(!strip !buildflags staticlibs !emptydirs)
makedepends=('android-configure')
groups=('android-${CARCH}-portlibs')
source=("https://github.com/fribidi/fribidi/archive/v${pkgver}.tar.gz")
md5sums=('2acb412d284f5918de3638c1052160dd')

prepare() {
  cd "${srcdir}"/fribidi-${pkgver}
  # patch out binaries, as they are not needed
  sed '/^SUBDIRS/ s/bin //' -i Makefile.am
  ./autogen.sh
}

build() {
  cd "${srcdir}"/fribidi-${pkgver}
  source /opt/pacbrew/android/android-env.sh ${CARCH}

  # TODO: fix flags
  export CFLAGS="-fPIC -O2"
  export CXXFLAGS="-fPIC -O2"
  export CPPFLAGS="-fPIC -O2"

  ./configure \
    --prefix="${ANDROID_PREFIX}" \
    --host=${CARCH}-linux-android \
    --target=${CARCH}-linux-android \
    --libdir=${ANDROID_PREFIX_LIB} \
    --includedir=${ANDROID_PREFIX_INCLUDE} \
    --disable-shared \
    --enable-static

  MAKEFLAGS="" make
}

package() {
  cd "${srcdir}"/fribidi-${pkgver}
  source /opt/pacbrew/android/android-env.sh ${CARCH}

  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/${ANDROID_PREFIX_SHARE}
  ${ANDROID_STRIP} -g "$pkgdir"/${ANDROID_PREFIX_LIB}/*.a
}

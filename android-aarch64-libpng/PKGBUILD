# Maintainer: Gonzalo Exequiel Pedone <hipersayan DOT x AT gmail DOT com>
# Contributor: Daniel Kirchner <daniel AT ekpyron DOT org>
# Contributor: cpasjuste <cpasjuste@gmail.com>

_android_arch=aarch64

pkgname=android-${_android_arch}-libpng
pkgver=1.6.37
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics (android)"
arch=('any')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=("android-${_android_arch}-zlib")
options=(!strip !buildflags staticlibs !emptydirs)
makedepends=('android-configure')
groups=('android-${_android_arch}-portlibs')
source=("http://downloads.sourceforge.net/sourceforge/libpng/libpng-$pkgver.tar.xz")
sha256sums=('505e70834d35383537b6491e7ae8641f1a4bed1876dbfe361201fc80868d88ca')

build() {
    cd "$srcdir/libpng-$pkgver"
    source /opt/pacbrew/android/android-env.sh ${_android_arch}

    android-configure \
        --enable-unversioned-links \
        --enable-unversioned-libpng-pc \
        --enable-unversioned-libpng-config

    make $MAKEFLAGS
}

package () {
    cd "${srcdir}/libpng-${pkgver}"
    source /opt/pacbrew/android/android-env.sh ${_android_arch}

    make DESTDIR="$pkgdir" install
    rm -r "${pkgdir}"/${ANDROID_PREFIX_SHARE}
    ${ANDROID_STRIP} -g "$pkgdir"/${ANDROID_PREFIX_LIB}/*.a
}


# Maintainer: Gonzalo Exequiel Pedone <hipersayan DOT x AT gmail DOT com>
# Contributor: cpasjuste <cpasjuste@gmail.com>

pkgname=android-${CARCH}-libvpx
pkgver=1.11.0
pkgrel=1
pkgdesc="VP8 and VP9 codec (android)"
arch=('any')
url="http://www.webmproject.org/"
license=('BSD')
depends=('android-ndk')
options=(!strip !buildflags staticlibs !emptydirs)
makedepends=('android-environment' 'android-pkg-config')
source=(https://github.com/webmproject/libvpx/archive/v${pkgver}.tar.gz)
md5sums=('82e5e527336b41281a582204db1f3457')
groups=('android-${CARCH}-portlibs')

prepare() {
    source /opt/pacbrew/android/android-env.sh ${CARCH}
    check_ndk_version_ge_than 18.0
}

build() {
    cd "${srcdir}"/libvpx-${pkgver}
    source /opt/pacbrew/android/android-env.sh ${CARCH}

    export CC=${ANDROID_CC}
    export CXX=${ANDROID_CXX}
    export STRIP=${ANDROID_STRIP}

    case "$CARCH" in
        x86*)
            ;;
        *)
            export AS=${ANDROID_AS}
            ;;
    esac

    target=${CARCH/aarch64/arm64}
    target=${target/armv7a-eabi/armv7}
    target=${target/x86-/x86_}
    target=${target}-android-gcc

    configue_opts="
        --target=${target}
        --prefix=${ANDROID_PREFIX}
        --libdir=${ANDROID_PREFIX_LIB}
        --enable-static
        --disable-shared
        --disable-install-bins
        --disable-docs
        --disable-install-docs
        --disable-install-srcs
        --enable-pic
        --enable-postproc
        --enable-vp8
        --enable-vp9
        --enable-vp9-highbitdepth
        --enable-vp9-temporal-denoising"

    case "$CARCH" in
        armv7a)
             configue_opts+="
                 --disable-neon_asm"
            ;;
        *)
            ;;
    esac

    ./configure ${configue_opts}
    make $MAKEFLAGS
}

package() {
    cd "${srcdir}"/libvpx-${pkgver}
    source /opt/pacbrew/android/android-env.sh ${CARCH}

    make DESTDIR="$pkgdir" install
    ${ANDROID_STRIP} -g "$pkgdir"/${ANDROID_PREFIX_LIB}/*.a
}

# Maintainer: Gonzalo Exequiel Pedone <hipersayan DOT x AT gmail DOT com>
# Contributor: cpasjuste <cpasjuste@gmail.com>

pkgname=android-${CARCH}-libvpx
pkgver=1.12.0
pkgrel=1
pkgdesc="VP8 and VP9 codec (android)"
arch=('any')
url="http://www.webmproject.org/"
license=('BSD')
depends=('android-ndk')
options=(!strip !buildflags staticlibs !emptydirs)
makedepends=('android-environment' 'android-pkg-config')
source=(https://github.com/webmproject/libvpx/archive/v${pkgver}.tar.gz)
md5sums=('10cf85debdd07be719a35ca3bfb8ea64')
groups=('android-${CARCH}-portlibs')

prepare() {
    source /opt/pacbrew/android/android-env.sh ${CARCH}
    check_ndk_version_ge_than 18.0
}

build() {
    cd "${srcdir}"/libvpx-${pkgver}
    source /opt/pacbrew/android/android-env.sh ${CARCH}

    case "$CARCH" in
        aarch64)
            target=arm64-android-gcc
            ;;
        armv7a)
            target=armv7-android-gcc
            ;;
        x86_64)
            target=x86_64-android-gcc
            export AS=""
            ;;
    esac

    configue_opts="
        --target=${target}
        --prefix=${ANDROID_PREFIX}
        --libdir=${ANDROID_PREFIX_LIB}
        --enable-static
        --disable-shared
        --disable-examples
        --disable-tools
        --disable-docs
        --disable-install-bins
        --disable-install-docs
        --disable-install-srcs
        --enable-pic
        --enable-postproc
        --enable-vp8
        --enable-vp9
        --enable-vp9-highbitdepth
        --enable-vp9-temporal-denoising"

    # TODO: fixme
    case "$CARCH" in
        armv7a)
             configue_opts+="
                 --disable-neon_asm"
            ;;
        *)
            ;;
    esac

    ./configure ${configue_opts}
    make $MAKEFLAGS
}

package() {
    cd "${srcdir}"/libvpx-${pkgver}
    source /opt/pacbrew/android/android-env.sh ${CARCH}

    make DESTDIR="$pkgdir" install
    ${ANDROID_STRIP} -g "$pkgdir"/${ANDROID_PREFIX_LIB}/*.a
}
